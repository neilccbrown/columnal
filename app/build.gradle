plugins {
    id 'application'
    id 'io.miret.etienne.sass' version '1.4.1'
}
apply plugin: 'java'


dependencies {
    implementation project(':utility')
    implementation project(':utility-adt')
    implementation project(':utility-error')
    implementation project(':utility-functional')
    implementation project(':utility-gui')
    implementation project(':parsers')
    implementation project(':data')
    implementation project(':stf')
    implementation project(':expressions')
    implementation project(':functions')
    implementation project(':identifiers')
    implementation project(':lex-editor')
    implementation project(':transformations')
    implementation project(':r-interop')
    implementation project(':types')
    implementation libs.guava
    implementation libs.wellbehavedfx
    //implementation 'com.google.code.findbugs:jsr305:3.0.2'
    implementation libs.controlsfx
    implementation libs.poi.ooxml
    implementation libs.jsoup
    implementation libs.commons.io
    implementation libs.commons.lang3
    implementation libs.commons.text
    implementation libs.javaSMT
    implementation libs.jna
    implementation libs.jna.platform
    //implementation libs.packr
}

task copySCSS(type: Copy) {
    from "${projectDir}/src/main/scss"
    from "${projectDir}/src/main/scss/light"
    include "*.scss"
    into "${buildDir}/generated/sources/scss/light"
}

compileSass {
    dependsOn copySCSS
    outputDir = project.file("${buildDir}/resources/main")
    sourceDir = project.file("${buildDir}/generated/sources/scss/light")
    noErrorCss()
}

task makeLogo(type: Exec) {
    workingDir "build/resources/main"
    // Sizes from https://docs.microsoft.com/en-us/windows/desktop/uxguide/vis-icons
    commandLine "magick", \
        "convert", \
        "../../../src/main/resources/logo.png", \
        "(", "+clone", "-resize", "256x256", "-write", "logo-256.png", "+delete", ")", \
        "(", "+clone", "-resize", "64x64", "-write", "logo-64.png", "+delete", ")", \
        "(", "+clone", "-resize", "48x48", "-write", "logo-48.png", "+delete", ")", \
        "(", "+clone", "-resize", "32x32", "-write", "logo-32.png", "+delete", ")", \
        "(", "+clone", "-resize", "24x24", "-write", "logo-24.png", "+delete", ")", \
        "-resize", "16x16", "logo-16.png"
}

// Gradle claims this as an implicit dependency but I don't understand why:
checkstyleMain.dependsOn(compileSass)
build.dependsOn(compileSass)
jar.dependsOn(compileSass)
run.dependsOn(compileSass)

build.dependsOn(makeLogo)
jar.dependsOn(makeLogo)
run.dependsOn(makeLogo)

sourceSets.main.resources.srcDirs('../xml/build/resources/main')

ext {
    javaMainClass = "xyz.columnal.gui.Main"
}
application {
    mainClass = javaMainClass
    applicationDefaultJvmArgs = ["-Dlog4j.configurationFile=dev-log4j2.xml", "--enable-preview"]
}
description = 'app'
